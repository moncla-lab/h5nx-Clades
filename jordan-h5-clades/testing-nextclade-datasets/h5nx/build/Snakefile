"""This file specifies the entire avian-flu pipeline that will be run, with
specific parameters for subsampling, tree building, and visualization. In this
build, you will generate 1 tree: an H5N1 tree for the HA genes. In this simple
build, clade annotation has been removed. This template should provide a
reasonable starting place to customize your own build. Simply edit and add
components to this Snakefile."""


"""Here, define your wildcards. To include more subtypes or gene segments, simply
add those to these lists, separated by commas"""
SUBTYPES = ["h5nx"]
SEGMENTS = ["ha"]

"""This rule tells Snakemak that at the end of the pipeline, you should have
generated JSON files in the auspice folder for each subtype and segment."""
rule all:
    input:
        auspice_json = expand("auspice/flu_avian_{subtype}_{segment}_nextclade.json", subtype=SUBTYPES, segment=SEGMENTS)

"""Specify all input files here. For this build, you'll start with input sequences
from the example_data folder, which contain metadata information in the
sequence header. Specify here files denoting specific strains to include or drop,
references sequences, and files for auspice visualization (lat_longs, colors)"""
rule files:
    params:
        input_sequences = "data/h5nx_ha.fasta",
        dropped_strains = "config/dropped_strains_h5nx.txt",
        include_strains = "config/include_strains_h5nx.txt",
        reference = "config/reference_h5nx_{segment}.gb",
        colors = "config/colors_h5nx.tsv",
        lat_longs = "config/lat_longs_h5nx.tsv",
        auspice_config = "config/auspice_config_h5nx.json",
        clade_defining_mutations = "results/clade_defining_mutations.tsv"

files = rules.files.params

"""The minimum length required for sequences. Sequences shorter than these will be
subsampled out of the build. Here, we're requiring all segments to be basically
complete. To include partial genomes, shorten these to your desired length"""
def min_length(w):
    len_dict = {"pb2": 2100, "ha":1100}
    length = len_dict[w.segment]
    return(length)

"""Sequences with sample collection dates earlier than these will be subsampled out of the build"""
def min_date(w):
    date = {'h5nx':'1990','h5n1': '1996'}
    return date['h5nx']

"""This function allows us to annotate HA sequences with cleavage site information,
without trying to apply it to the other segments"""
def node_data_by_wildcards(w):
    """for ha, include cleavage site data during export; for other segments, do not"""
    if w.segment == "ha":
        node_data = [rules.refine.output.node_data,rules.traits.output.node_data,rules.ancestral.output.node_data,rules.translate.output.node_data,rules.cleavage_site.output.cleavage_site_annotations,rules.cleavage_site.output.cleavage_site_sequences, rules.augur_clades.output.clade_assignments]
    else:
        node_data = [rules.refine.output.node_data,rules.traits.output.node_data,rules.ancestral.output.node_data,rules.translate.output.node_data]
    return(node_data)


"""In this section of the Snakefile, rules are specified for each step of the pipeline.
Each rule has inputs, outputs, parameters, and the specific text for the commands in
bash. Rules reference each other, so altering one rule may require changing another
if they depend on each other for inputs and outputs. Notes are included for
specific rules."""


"""The parse rule is used to separate out sequences and metadata into 2 distinct
files. This rule assumes an input fasta file that contains metadata information
in the header. By specifying the order of those fields in the `fasta_fields` line,
`augur parse` will separate those fields into labeled columns in the output metadata
file."""
rule parse:
    message: "Parsing fasta into sequences and metadata"
    input:
        sequences = files.input_sequences
    output:
        sequences = "results/sequences_{subtype}_{segment}.fasta",
        metadata = "results/metadata_{subtype}_{segment}.tsv"
    params:
        fasta_fields =  "strain date h5_subtype clade",
    shell:
        """
        augur parse \
            --sequences {input.sequences} \
            --output-sequences {output.sequences} \
            --output-metadata {output.metadata} \
            --fields {params.fasta_fields} \
        """

"""This rule specifies how to subsample data for the build, which is highly
customizable based on your desired tree."""
rule filter:
    message:
        """
        Filtering to
          - {params.sequences_per_group} sequence(s) per {params.group_by!s}
          - excluding strains in {input.exclude}
          - samples with missing region and country metadata
          - excluding strains prior to {params.min_date}
        """
    input:
        sequences = rules.parse.output.sequences,
        metadata = rules.parse.output.metadata,
        exclude = files.dropped_strains,
        include = files.include_strains
    output:
        sequences = "results/filtered_{subtype}_{segment}.fasta"
    params:
        group_by = "clade",
        sequences_per_group = 1000,
        min_date = min_date,
        min_length = min_length
        #exclude_where = "host=laboratoryderived host=ferret host=unknown host=other country=? region=?"

    shell:
        """
        augur filter \
            --sequences {input.sequences} \
            --metadata {input.metadata} \
            --exclude {input.exclude} \
            --include {input.include} \
            --output {output.sequences} \
            --group-by {params.group_by} \
            --sequences-per-group {params.sequences_per_group} \
            --min-date {params.min_date} \
            --min-length {params.min_length} \
            --non-nucleotide
        """

rule align:
    message:
        """
        Aligning sequences to {input.reference}
          - filling gaps with N
        """
    input:
        sequences = rules.filter.output.sequences,
        reference = files.reference
    output:
        alignment = "results/aligned_{subtype}_{segment}.fasta"
    shell:
        """
        augur align \
            --sequences {input.sequences} \
            --reference-sequence {input.reference} \
            --output {output.alignment} \
            --nthreads 1
        """

rule aa_align:
    message:
        """
        Translating nucleotide alignment into amino acid alignment
        """
    input:
        nt_alignment = rules.align.output.alignment,
        reference = files.reference
    output:
        aa_alignment = "results/aa-aligned_{subtype}_{segment}.fasta"
    shell:
        """
        python scripts/aa-alignment.py \
            --nt_alignment {input.nt_alignment} \
            --reference {input.reference} \
            --aa_alignment {output.aa_alignment}
        """

rule tree:
    message: "Building tree"
    input:
        alignment = rules.align.output.alignment
    output:
        tree = "results/tree-raw_{subtype}_{segment}.nwk"
    params:
        method = "iqtree"
    shell:
        """
        augur tree \
            --alignment {input.alignment} \
            --output {output.tree} \
            --method {params.method} \
            --nthreads 1
        """

rule refine:
    message:
        """
        Refining tree
          - estimate timetree
          - use {params.coalescent} coalescent timescale
          - estimate {params.date_inference} node dates
        """
    input:
        tree = rules.tree.output.tree,
        alignment = rules.align.output,
        metadata = rules.parse.output.metadata
    output:
        tree = "results/tree_{subtype}_{segment}.nwk",
        node_data = "results/branch-lengths_{subtype}_{segment}.json"
    params:
        coalescent = "const",
        date_inference = "marginal",
        clock_filter_iqd = 8
    shell:
        """
        augur refine \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --metadata {input.metadata} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data} \
            --timetree \
            --coalescent {params.coalescent} \
            --date-confidence \
            --date-inference {params.date_inference} \
            --clock-filter-iqd {params.clock_filter_iqd}
        """

rule ancestral:
    message: "Reconstructing ancestral sequences and mutations"
    input:
        tree = rules.refine.output.tree,
        alignment = rules.align.output
    output:
        node_data = "results/nt-muts_{subtype}_{segment}.json"
    params:
        inference = "joint"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --inference {params.inference}\
            --keep-ambiguous
        """

rule translate:
    message: "Translating amino acid sequences"
    input:
        tree = rules.refine.output.tree,
        node_data = rules.ancestral.output.node_data,
        reference = files.reference
    output:
        node_data = "results/aa-muts_{subtype}_{segment}.json"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data}
        """

rule traits:
    message: "Inferring ancestral traits for {params.columns!s}"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata
    output:
        node_data = "results/traits_{subtype}_{segment}.json",
    params:
        columns = "clade",
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence
        """

"""This is a custom rule developed for the avian influenza builds and is not part
of the Nextstrain architecture. It uses custom python scripts to determine the
sequence of amino acids at the HA cleavage site, and annotate those sequences
for whether they contain a furin cleavage site."""
rule cleavage_site:
    message: "determining sequences that harbor furin cleavage sites"
    input:
        alignment = "results/aligned_{subtype}_ha.fasta"
    output:
        cleavage_site_annotations = "results/cleavage-site_{subtype}_ha.json",
        cleavage_site_sequences = "results/cleavage-site-sequences_{subtype}_ha.json"
    shell:
        """
        python scripts/annotate-ha-cleavage-site.py \
            --alignment {input.alignment} \
            --furin_site_motif {output.cleavage_site_annotations} \
            --cleavage_site_sequence {output.cleavage_site_sequences}
        """


"""This rule exports the results of the pipeline into JSON format, which is required
for visualization in auspice. To make changes to the categories of metadata
that are colored, or how the data is visualized, alter the auspice_config files"""
rule export:
    message: "Exporting data files for for auspice"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata,
        node_data = [rules.refine.output.node_data,rules.traits.output.node_data,rules.ancestral.output.node_data,rules.translate.output.node_data,rules.cleavage_site.output.cleavage_site_annotations,rules.cleavage_site.output.cleavage_site_sequences],
        colors = files.colors,
        lat_longs = files.lat_longs,
        auspice_config = files.auspice_config
    output:
        auspice_json = "auspice/flu_avian_{subtype}_{segment}.json"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.node_data}\
            --colors {input.colors} \
            --lat-longs {input.lat_longs} \
            --auspice-config {input.auspice_config} \
            --include-root-sequence \
            --output {output.auspice_json}
        """




rule clade_muts:
    message: "determining clade-defining mutations"
    input:
        baltic = "scripts/baltic.py",
        tree = rules.export.output.auspice_json,
        excluded_tips = "config/clades/excluded_tips.txt",
        outgroup_clades = "config/clades/outgroup_clades.txt",
        init_mutations = "config/clades/init_mutations.tsv"
    output:
        clade_mutations = "results/clade_defining_mutations_{subtype}_{segment}.tsv"
    shell:
        """
        python scripts/clade-defining-mutations.py \
            --baltic {input.baltic} \
            --tree {input.tree} \
            --clade_mutations {output.clade_mutations} \
            --excluded_tips {input.excluded_tips} \
            --outgroup_clades {input.outgroup_clades} \
            --init_mutations {input.init_mutations}
        """

rule augur_clades:
    message: "assigning clades with augur clades"
    input:
        tree = rules.refine.output.tree,
        aa_muts = rules.translate.output.node_data,
        nt_muts = rules.ancestral.output.node_data,
        clade_mutations = rules.clade_muts.output.clade_mutations
    output:
        clade_assignments = "results/clade_assignments_{subtype}_{segment}.json"
    shell:
        """
        augur clades \
            --tree {input.tree} \
            --mutations {input.aa_muts} {input.nt_muts}\
            --clades {input.clade_mutations} \
            --output-node-data {output.clade_assignments}
        """

rule root_muts:
    message: "attaching root mutations to tree"
    input:
        tree = rules.refine.output.tree,
        translations = rules.aa_align.output.aa_alignment,
        nuc_mutations = rules.ancestral.output.node_data,
        aa_mutations = rules.translate.output.node_data
    output:
        nuc_mutations = "results/aa-muts_{subtype}_{segment}_rootupdated.json",
        aa_mutations = "results/nt-muts_{subtype}_{segment}_rootupdated.json"
    params:
        reference = "A/Goose/Guangdong/1/96",
        genes = "HA"
    shell:
        """
        python scripts/attach-root-mutations.py \
            --tree {input.tree} \
            --reference {params.reference} \
            --translations {input.translations} \
            --genes {params.genes} \
            --nuc-mutations {input.nuc_mutations} \
            --aa-mutations {input.aa_mutations} \
            --output-nuc-mutations {output.nuc_mutations} \
            --output-aa-mutations {output.aa_mutations}
        """

rule export2:
    message: "exporting data files (with clade assignments and root mutations) for auspice"
    input:
        tree = rules.refine.output.tree,
        metadata = rules.parse.output.metadata,
        node_data = [rules.refine.output.node_data,rules.traits.output.node_data,rules.root_muts.output.nuc_mutations,rules.root_muts.output.aa_mutations,rules.cleavage_site.output.cleavage_site_annotations,rules.cleavage_site.output.cleavage_site_sequences, rules.augur_clades.output.clade_assignments],
        colors = files.colors,
        lat_longs = files.lat_longs,
        auspice_config = files.auspice_config
    output:
        auspice_json = "auspice/flu_avian_{subtype}_{segment}_nextclade.json"
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --node-data {input.node_data}\
            --colors {input.colors} \
            --lat-longs {input.lat_longs} \
            --auspice-config {input.auspice_config} \
            --include-root-sequence \
            --output {output.auspice_json}
        """





rule clean:
    message: "Removing directories: {params}"
    params:
        "results ",
        "auspice"
    shell:
        "rm -rfv {params}"

